; Packs the null terminated string at A into B.
; B will be padded by null characters to reach a word boundary.
; Sets C to the number of words written to B
;   A - Source memory location
;   B - Destination memory location
:str_pack8
  SET PUSH, X
  SET PUSH, Y
  SET C, 0

:str_pack8_loop
  ; set high part
  SET X, [A]
  SHL X, 8

  ; write to B
  SET [B], X
  ADD C, 1
  SET Y, B
  ADD B, 1

  ; check for terminator
  IFE [A], 0x0
  SET PC, str_pack8_loop_end

  ; set low part
  ADD A, 1
  XOR [Y], [A]

  ; check for terminator
  IFE [A], 0x0
  SET PC, str_pack8_loop_end

  ; next character
  ADD A, 1
  SET PC, str_pack8_loop

:str_pack8_loop_end
  SET Y, POP
  SET X, POP
  SET PC, POP

; Unpacks the null terminated string at A into B.
; Sets C to the number of words written to B
;   A - Source memory location
;   B - Destination memory location
:str_unpack8
  SET PUSH, X
  SET PUSH, Y
  SET C, 0

:str_unpack8_loop
  ; read high character into X
  SET X, [A]
  AND X, 0xff00
  SHR X, 8

  ; add to B
  SET [B], X
  SET Y, A
  ADD A, 1
  ADD B, 1
  ADD C, 1

  ; check if high character is the terminator
  IFE X, 0x0
  SET PC, str_unpack8_loop_end

  ; read low character into X
  SET X, [Y]
  AND X, 0x00ff

  ; add to B
  SET [B], X
  ADD B, 1
  ADD C, 1

  ; check if the low character is the terminator
  IFE X, 0
  SET PC, str_unpack8_loop_end

  ; next character
  SET PC, str_unpack8_loop

:str_unpack8_loop_end
  SET Y, POP
  SET X, POP
  SET PC, POP
